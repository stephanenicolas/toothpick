buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath ('com.android.tools.build:gradle:3.0.0-beta7')
  }
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId 'com.example.smoothie'
    minSdkVersion 15
    targetSdkVersion 26
    versionCode 1
    versionName '1.0.0'
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
          'toothpick_registry_package_name': 'com.example.smoothie',
        ]
      }
    }
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
    abortOnError false
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

repositories {
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
  implementation project(path: ':smoothie', configuration: 'default')
  implementation project(':toothpick-runtime')
  implementation deps.butterknife
  implementation deps.rxandroid

  annotationProcessor project(':toothpick-compiler')
  annotationProcessor deps.butterknife_compiler

  testImplementation project(':toothpick-testing')
  testImplementation deps.junit
  testImplementation deps.easymock
  testImplementation deps.truth
  testImplementation deps.robolectric

  androidTestImplementation deps.espresso
  androidTestImplementation deps.easymock
  androidTestImplementation deps.dexmaker
}
